model {

  # Specify the priors for all parameters in the model
  lambda[1] ~ dunif(0, 30)
  lambda[2] ~ dunif(0, 30)
  lambda[3] ~ dunif(0, 30)
  lambda[4] ~ dunif(0, 30)
  
  gamma[1] ~ dunif(0, 5)
  gamma[2] ~ dunif(0, 5)

  theta[1] ~ dunif(0, 1)
  theta[2] ~ dunif(0, 1)
  theta[3] ~ dunif(0, 1)
    
  omega[1] ~ dunif(0, 1)
  omega[2] ~ dunif(0, 1)
  omega[3] ~ dunif(0, 1)
  omega[4] ~ dunif(0, 1)

  #Create a loop across all j sites
  for(j in 1:nSites)
  {
    #Intitate the model for year 1 - poisson with parameter lambda
    #The abundance matrix N is specified location x year x stage
    N[j,1,1] ~ dpois(lambda[1])
    N[j,1,2] ~ dpois(lambda[2])
    N[j,1,3] ~ dpois(lambda[3])
    N[j,1,4] ~ dpois(lambda[4])

    #Create S and G vectors for year 1, which are not used in the model
    #S and G for year one are set to be consistent with the intial values
    for (r in 1:4)
    {
      S[j, 1, r] ~ dpois(2)  
      G[j, 1, r] ~ dpois(1)
    }

    #Specify the model for years 2 through nYears
    for(t in 2:nYears)
    { 
      # Get abundance from neighbour adults (+ traget site)
      Neig[j, t-1] <- sum(N[neighbour[j, 1:lastNonNa[j]], t-1, 4])
     
      #Estimate survivorship
      S[j,t,1] ~ dbin(omega[1], N[j,t-1,1]) 
      S[j,t,2] ~ dbin(omega[2], N[j,t-1,2])
      S[j,t,3] ~ dbin(omega[3], N[j,t-1,3])
      S[j,t,4] ~ dbin(omega[4], N[j,t-1,4])
      
      #Estimate recruitment for sapling and transition to next stage
      G[j,t,1] ~ dpois(gamma[1] * Neig[j,t-1])            # N_1 recruit
      G[j,t,2] ~ dbin(theta[1], S[j,t,1])                 # Transition from N_1 to N_2
      G[j,t,3] ~ dpois(gamma[2])                          # N_2 recruit (Individuals growthing too fast that were not indentified on first N_1 stage)
      G[j,t,4] ~ dbin(theta[2], S[j,t,2])                 # Transition from N_2 to N_3
      G[j,t,5] ~ dbin(theta[3], S[j,t,3])                 # Transition from N_3 to N_4

      #Sum all stages to get total N at each site j in each year t
      N[j,t,1] <- S[j,t,1] + G[j,t,1] - G[j,t,2]
      N[j,t,2] <- S[j,t,2] + G[j,t,2] + G[j,t,3] - G[j,t,4]
      N[j,t,3] <- S[j,t,3] + G[j,t,4] - G[j,t,5]
      N[j,t,4] <- S[j,t,4] + G[j,t,5]
    }

    #Loop accross years
    #The data matrix n is specified site, time, stage
    for (t in 1:nYears)
    {
      n[j,t,1] ~ dpois(N[j,t,1])
      n[j,t,2] ~ dpois(N[j,t,2])
      n[j,t,3] ~ dpois(N[j,t,3])
      n[j,t,4] ~ dpois(N[j,t,4])
    } 
  }
  
  #sum up the number of individuals in all locations to estimate
  #total N for each stage
  for (t in 1:nYears)
  {
    Ntotal[1,t] <- sum(N[,t,1])
    Ntotal[2,t] <- sum(N[,t,2])
    Ntotal[3,t] <- sum(N[,t,3])
    Ntotal[4,t] <- sum(N[,t,4])
  }  
}
